plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id "io.freefair.lombok" version "${lombokPluginVersion}"
    id "au.com.dius.pact" version "${pactVersion}"
}

group 'com.joe.bff'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(jdkVersion)
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudBomVersion}"
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "au.com.dius.pact.consumer:junit5:${pactVersion}"
}

test {
    useJUnitPlatform()
    finalizedBy pactPublish
}

jar {
    enabled = false
}

pact {
    /**
     * Documentation for the properties can be found here
     *
     * https://github.com/pact-foundation/pact-jvm/tree/4_3_15/provider/gradle
     * https://github.com/pact-foundation/pact-jvm/tree/4_3_15/provider/gradle/src/main/kotlin/au/com/dius/pact/provider/gradle
     */

    publish {
        pactBrokerUrl = 'https://test.pactflow.io/'
        pactBrokerUsername = 'dXfltyFMgNOFZAxr8io9wJ37iUpY42M'
        pactBrokerPassword = 'O5AIZWxelWbLvqMd8PkAVycBJh2Psyg1'
        consumerVersion = project.version
        consumerBranch = getGitBranch()
        tags = ['production']
        consumerBuildUrl = 'https://jenkins.com/build/bff'
    }
}